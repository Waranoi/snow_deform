#version 450 core
#define PI 3.1415926538

layout(quads, equal_spacing, ccw) in;

in vec3 es_pos[];
in vec3 es_frag_pos[];
in vec3 es_norm[];
in vec2 es_tex_coord[];

out VERTEX
{
    vec3 frag_pos;
    vec3 norm;
    vec2 tex_coord;
    vec3 color;
} vertex;

uniform float radius = 0;
uniform mat4 proj;
uniform mat4 view;
uniform float snow_height;
layout (binding = 1) uniform sampler2D height_map;

void main()
{
    // Used for debug_normals_gs geometry shader
    vertex.color = vec3(0);
    
    // Interpolate texture coordinates
    float t1 = mix(es_tex_coord[0].x, es_tex_coord[1].x, gl_TessCoord.x);
    float t2 = mix(es_tex_coord[0].y, es_tex_coord[3].y, gl_TessCoord.y);
    vertex.tex_coord = vec2(t1, t2);

    vec3 p;
    if (radius > 0)
    {
        float lat = vertex.tex_coord.x * 2 * PI;
        float lon = vertex.tex_coord.y * PI;

        float x = radius * cos(lat) * sin(lon);
        float y = radius * sin(lat) * sin(lon);
        float z = radius * cos(lon);
        p = vec3(x, y, z);
        vertex.norm = normalize(p);
    }
    else
    {
        // Interpolate along bottom edge using x component of the
        // tessellation coordinate
        vec3 p1 = mix(es_pos[0], es_pos[1], gl_TessCoord.x);
        // Interpolate along top edge using x component of the
        // tessellation coordinate
        vec3 p2 = mix(es_pos[3], es_pos[2], gl_TessCoord.x);
        // Now interpolate those two results using the y component
        // of tessellation coordinate
        p = mix(p1, p2, gl_TessCoord.y);
        vertex.norm = es_norm[0];
    }

    float height_offset = texture(height_map, vertex.tex_coord).x * snow_height;

    vec3 final_pos = p + vertex.norm * height_offset;
    gl_Position = proj * view * vec4(final_pos, 1);

    // Calculate frag pos
    vec3 fp1 = mix(es_frag_pos[0], es_frag_pos[1], gl_TessCoord.x);
    vec3 fp2 = mix(es_frag_pos[3], es_frag_pos[2], gl_TessCoord.x);
    vec3 fp = mix(fp1, fp2, gl_TessCoord.y);
    vertex.frag_pos = fp + vertex.norm * height_offset;
}